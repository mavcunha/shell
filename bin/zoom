#!/usr/bin/env zsh

typeset -gA meeting # meeting id associative array
typeset -ga mtg=()

function _help() {
  echo "

Usage:

  zoom [-h|--help] [-d|--dry] ZOOMID

A file called ${HOME}/.zoomids which should contain
your meetings IDs, create one with your meeting IDs using the
following template:

meeting[MEETING_NAME]=[ID | ID_AND_PASS | URL]

Examples (all are valid inputs):

meeting[standup]=111111111
meeting[standup]=111111111:aaaAAAaaaAAA
meeting[standup]=https://yourcompany.zoom.us/j/1111111?pwd=aaaAAAaaaAAA

If called directly with a number 'zoom' will assume it is
a meeting ID and will launch the client with the number.

NOTEs:
  The password must be already hashed.
  MEETING_NAME cannot be a number as the script will launch a number
  as if was a meeting id.
"
exit 1
}

function _show_meetings() {
  echo "Configured meetings"
  for key val in "${(@kv)meeting}"; do
    printf "  %.10s %-60s\n" \
       "${key}                                  " \
       "${val}"

  done
}

function _open_meeting() {
  local meeting_id="${1}"
  local meeting_pass="${2}"
  echo "Opening Meeting ID ${meeting_id}"

  if [[ ${meeting_id} =~ ^http ]]; then
    url="${meeting_id}:${meeting_pass}"
  else
    url="zoommtg://zoom.us/join?action=join&confno=${meeting_id}"
    [[ -n ${meeting_pass} ]] && url="${url}&pwd=${meeting_pass}"
  fi

  if [[ -n ${DEBUG} ]]; then
    echo open -g "${url}"
  else
    open "${url}"
  fi
}

function _split_arg() {
  local arg="${1}"

  # remove all spaces
  meeting_argument=${arg// /}

  # parse argument as array, split number from password
  echo "${(@s/:/)meeting_argument}"
}

# pre-check if we have a configuration
[[ ! -f ${HOME}/.zoomids ]] && _help

# load configured meeting ids
. ${HOME}/.zoomids

# show help if asked to do so
case "${1}" in
  -h|--help)
    _help
  ;;
  -d|--dry)
    DEBUG="true"
    shift
  ;;
esac

mtg=($(_split_arg "${*}"))

if [[ -z ${mtg[1]} ]]; then
  _show_meetings
elif [[ -n ${meeting[${mtg[1]}]} ]]; then
  _open_meeting $(_split_arg "${meeting[${mtg[1]}]}")
elif echo ${mtg[1]} | grep -q '^[-0-9]\+$'; then
  _open_meeting ${mtg[1]}
else
  echo "Meeting ID for '${mtg[1]}' not found and is not a meeting number."
  _show_meetings
fi
